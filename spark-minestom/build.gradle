import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java-library'
    id 'maven-publish'
}

tasks.withType(JavaCompile) {
    // override, compile targeting J21
    options.release = 21
}

dependencies {
    var minestom = "11d7530dfc"

    api "me.lucko:spark-common:${project.baseVersion}-SNAPSHOT"

    compileOnly "net.minestom:minestom-snapshots:$minestom"
    implementation "com.google.guava:guava:33.3.1-jre"

    testImplementation "net.minestom:minestom-snapshots:$minestom"
    testImplementation "ch.qos.logback:logback-classic:1.5.12"
}

task processSource(type: Sync) {
    from sourceSets.main.java
    filter(ReplaceTokens, tokens: [VERSION: project.version])
    into project.getLayout().getBuildDirectory().get().dir("src")
}

compileJava {
    source = processSource.outputs
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    repositories {

    }

    publications {
        mavenJava(MavenPublication) {
            artifactId = 'spark-minestom'

            from components.java

            pom {
                name = 'Spark Minestom'
                description = 'A performance profiler for Minestom servers.'
                url = 'https://github.com/LooFifteen/spark'

                licenses {
                    license {
                        name = 'GNU General Public License version 3'
                        url = 'https://opensource.org/license/gpl-3-0'
                    }
                }

                developers {
                    developer {
                        id = 'lucko'
                        name = 'Luck'
                        url = 'https://lucko.me'
                        email = 'git@lucko.me'
                    }
                    developer {
                        id = 'LooFifteen'
                        name = 'LU15'
                        url = 'https://lu15.dev'
                        email = 'luis@lu15.dev'
                    }
                }

                scm {
                    connection = 'scm:git:https://github.com/LooFifteen/spark.git'
                    developerConnection = 'scm:git:git@github.com:LooFifteen/spark.git'
                    url = 'https://github.com/LooFifteen/spark'
                }

                issueManagement {
                    system = 'GitHub'
                    url = 'https://github.com/LooFifteen/spark/issues'
                }
            }
        }
    }
}
